plugins {
	id 'org.springframework.boot' version '2.5.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id "com.ewerk.gradle.plugins.querydsl" version "1.0.10"
	id 'java'
	id 'jacoco'
	// *** Q-Type error 때문에 추가 *** //
	id "io.franzbecker.gradle-lombok" version "3.0.0"
}

group = 'com.server'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	// *** Spring Starter *** //
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'	// data JPA
	implementation 'org.springframework.boot:spring-boot-starter-data-redis' //redis
	implementation 'org.springframework.boot:spring-boot-starter-security' //security
	implementation 'org.springframework.boot:spring-boot-starter-validation' //validation
	implementation 'org.springframework.boot:spring-boot-starter-web' 	// starter web

	// *** jwt *** //
	implementation 'io.jsonwebtoken:jjwt-api:0.11.1'
	implementation group: 'io.jsonwebtoken', name: 'jjwt', version: '0.2'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.1'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.1'

	// *** swagger *** //
	implementation 'io.springfox:springfox-swagger2:2.9.2'
	implementation 'io.springfox:springfox-swagger-ui:2.9.2'

	// *** h2 *** //
	runtimeOnly 'com.h2database:h2'

	// *** dev tool and test *** //
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'

	// *** lombok *** //
	implementation 'org.projectlombok:lombok'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testAnnotationProcessor 'org.projectlombok:lombok'
	testCompileOnly 'org.projectlombok:lombok'

	// *** querydsl *** //
	implementation 'com.querydsl:querydsl-jpa'

	// *** 다국어 처리를 위한 yaml-resource-bundle *** //
	implementation 'net.rakugakibox.util:yaml-resource-bundle:1.2'

	// *** sms를 이용하기 위한 javaSDK *** //
	implementation group: 'net.nurigo', name: 'javaSDK', version: '2.2'
	compileOnly group: 'net.nurigo', name: 'javaSDK', version: '2.2'
	annotationProcessor group: 'net.nurigo', name: 'javaSDK', version: '2.2'
	testAnnotationProcessor group: 'net.nurigo', name: 'javaSDK', version: '2.2'
	testCompileOnly group: 'net.nurigo', name: 'javaSDK', version: '2.2'
}

test {
	jacoco {
		enabled = true
		destinationFile = file("$buildDir/jacoco/${name}.exec")
		includes = []
		excludes = []
		excludeClassLoaders = []
		includeNoLocationClasses = false
		sessionId = "<auto-generated value>"
		dumpOnExit = true
		classDumpDir = null
		output = JacocoTaskExtension.Output.FILE
		address = "localhost"
		port = 6300
		jmx = false
	}
	useJUnitPlatform()
	finalizedBy 'jacocoTestReport'
}

//querydsl 추가 시작
def querydslDir = "$buildDir/generated/querydsl"
querydsl {
	jpa = true
	querydslSourcesDir = querydslDir
}
sourceSets {
	main.java.srcDir querydslDir
}
configurations {
	querydsl.extendsFrom compileClasspath
}
compileQuerydsl {
	options.annotationProcessorPath = configurations.querydsl
}
//querydsl 추가 끝

jacoco {
	toolVersion = '0.8.7'
}

jacocoTestReport {
	reports {
		html.enabled true
		csv.enabled false
		xml.enabled false
	}
	afterEvaluate {
		classDirectories.from = files(classDirectories.files.collect {
			fileTree(dir: it, exclude: [
					'**/exception/**'
					,'**/dto/**'
					,'**/EZY/EzyApplication*'
					,'**/EZY/**/*Test*'
					,'**/EZY/**/Q*' // Q class 제거
			])
		})
	}
	finalizedBy 'jacocoTestCoverageVerification'
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			// 룰을 키고 끌 수 있다.
			enabled = true

			// 룰을 체크할 단위
			element = 'BUNDLE'

			// Java 바이트코드 를 통한 코드실행
			limit {
				counter = 'INSTRUCTION'
				value = 'COVEREDRATIO'
				minimum = 0.50
			}
		}
	}
}
